@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Agency Master Data'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZATS_XX_U_AGENCY as select from /dmo/agency as agency
association[1] to I_Country as _Country on
$projection.CountryCode = _Country.Country
{
    key agency.agency_id as AgencyId,
        agency.name as Name,
        agency.street as Street,
        agency.postal_code as PostalCode,
        agency.city as City,
        agency.country_code as CountryCode,
        agency.phone_number as PhoneNumber,
        agency.email_address as EmailAddress,
        agency.web_address as WebAddress,
        _Country  
}



@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Customer Master Data'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZATS_XX_U_CUSTOMER as select from /dmo/customer as customer 
association[1] to I_Country as _Country
on $projection.CountryCode = _Country.Country
{
   key customer.customer_id as CustomerId,
   concat(concat(title, concat(' ', first_name)), concat(' ',last_name)) as CustomerName,
   customer.city as City,
   customer.country_code as CountryCode,
   customer.phone_number as PhoneNumber,
   customer.email_address as EmailAddress,
   _Country 
}



@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Travel Entity (root of BO)'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZATS_XX_U_TRAVEL as select from /dmo/travel as Travel 
association[1] to ZATS_XX_U_AGENCY as _Agency
on $projection.AgencyId = _Agency.AgencyId
association[1] to ZATS_XX_U_CUSTOMER as _Customer
on $projection.CustomerId = _Customer.CustomerId
association[1] to I_Currency as _Currency
on $projection.CurrencyCode = _Currency.Currency
{
    key Travel.travel_id as TravelId,
    Travel.agency_id as AgencyId,
    Travel.customer_id as CustomerId,
    Travel.begin_date as BeginDate,
    Travel.end_date as EndDate,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    Travel.booking_fee as BookingFee,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    Travel.total_price as TotalPrice,
    Travel.currency_code as CurrencyCode,
    Travel.description as Description,
    Travel.status as Status,
    Travel.createdby as Createdby,
    Travel.createdat as Createdat,
    Travel.lastchangedby as Lastchangedby,
    Travel.lastchangedat as Lastchangedat,
    _Agency,
    _Customer,
    _Currency
}



@EndUserText.label: 'Service definition'
define service ZATS_XX_U_TRAVEL_SD {
  expose ZATS_XX_U_TRAVEL as Travel;
  expose ZATS_XX_U_CUSTOMER as Customer;
  expose ZATS_XX_U_AGENCY as Agency;
}

